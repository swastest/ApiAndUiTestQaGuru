plugins {
    id 'java-library'
    id "io.qameta.allure" version "2.9.6"
}

repositories {
    mavenCentral()
}
def junitVersion = "5.8.2",
    slfVersion = "1.7.36",
    allureVersion = "2.17.3",
    restAssuredVersion = "4.5.1",
    lombokVersion = "1.18.10",
    selenideVersion = "6.5.0",
    fakerVersion = "1.0.2"


allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

dependencies {
    testImplementation(

            "io.qameta.allure:allure-selenide:$allureVersion",
            "io.qameta.allure:allure-rest-assured:$allureVersion",
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            "org.slf4j:slf4j-simple:$slfVersion",
            "io.rest-assured:rest-assured:$restAssuredVersion",
            "org.projectlombok:lombok:$lombokVersion",
            'org.aeonbits.owner:owner:1.0.4',
            'com.google.code.gson:gson:2.8.9',
            'com.github.javafaker:javafaker:1.0.2',
            "com.codeborne:selenide:$selenideVersion",
            "com.github.javafaker:javafaker:$fakerVersion"
    )
}

test {
    useJUnitPlatform()
}


compileTestJava {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    systemProperties(System.getProperties())
    useJUnitPlatform()

    if (System.getProperty("threads") != null) {
        systemProperties += [
                'junit.jupiter.execution.parallel.enabled'                 : true,
                'junit.jupiter.execution.parallel.mode.default'            : 'concurrent',
                'junit.jupiter.execution.parallel.mode.classes.default'    : 'concurrent',
                'junit.jupiter.execution.parallel.config.strategy'         : 'fixed',
                'junit.jupiter.execution.parallel.config.fixed.parallelism': System.getProperty("threads").toInteger()
        ]
    }


    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

task demoweb(type: Test) {
    useJUnitPlatform {
        includeTags "demoweb"
    }
}
task bugred(type: Test) {
    useJUnitPlatform {
        includeTags "bugred"
    }
}

